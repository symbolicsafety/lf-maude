*** This is a model of the simple train door given on Figure 2 in the Verification of Cyberphysical Systems paper.

in lf-representation

omod TRAINDOOR is 
 including LF-REPR .
 
 ops moving locked paexternal : -> VarId [ctor] .
 ops controller train door : -> ReactorId [ctor] .
 ops inMove inLock outMove outLock : -> PortId [ctor] .
 ops startup external : -> ActionId .
 ops counter1 counter2 : -> VarId [ctor] .
 op init : -> Configuration .
 
 eq init = 
    < controller : Reactor | 
      inports : none,
      outports : < outLock : Port | value : [false] > 
                 < outMove : Port | value : [false] >, 
      state : empty,
      timers : none,
      actions : < startup : LogicalAction | minDelay : 0, 
                               minSpacing : 0, 
                               policy : defer, 
                               payload : [0] >
                < external : PhysicalAction | 
                               minDelay : 0, 
                               minSpacing : 0, 
                               policy : defer, 
                               payload : [false] >,
      reactions : 
        (reaction when startup do { schedule(external, [3], [false]) })
        reaction when external --> outLock ; outMove do
	{ (outLock <- [external] ) ; (outMove <- [external]) } 
    >
    
    < train : Reactor | 
        inports : < inMove : Port | value : [false] >,
        outports : none,
        state : (moving |-> [false]) ; (counter1 |-> [0]), 
        timers : none,
        actions : none,
        reactions : reaction when inMove do {(counter1 := counter1 + [1]) ;
	                                     moving := [inMove] }       
    >
    
    < door : Reactor | 
        inports : < inLock : Port | value : [false] >,
        outports : none,
        state : (locked |-> [false])  ; (counter2 |-> [0]),
        timers : none,
        actions : none,
        reactions : reaction when inLock do { (counter2 := counter2 + [1]) ;
	                                      locked := [inLock] }       
    >

    (controller : outLock --> door : inLock)
    controller : outMove --> train : inMove .
endom
