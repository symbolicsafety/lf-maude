
--- This file is almost the same as the one in dynamics-concrete.maude, except that 
---  1) physicalActions do not have timers 
---  3) an extra object for (global) symbolic constraints

omod DYNAMICS-WITHOUT-TICK is
  including ABS-DYNAMICS-WITHOUT-TICK .
  including EXECUTE-STEP .
  including EVENT-QUEUE .
  including PHYSICAL-ACTIONS .

  --- At the moment, the global state is
  --- < env : Environment | physicalActions : ... >
  --- REACTORS and CONNECTIONS in some state
  --- < q : EventQueue | queue : ... >
  --- < sym : SymInfra | constraint : ..., counter : ... >

  class SymInfra | constraint : BoolValue,
                   counter : Nat .


  vars REACTORS-AND-CONNECTIONS NEW-NETWORK : Configuration .
  vars QUEUE NEW-QUEUE : EQueue .
  vars E Q RXNS SYM : Oid .
  var INVOKED : ReactionIdSet .
  var EVENTS : Events .
  vars CONST CONST' CONST'' : BoolValue .
  var TAG : Tag .
  var TI : TimeInf .
  vars T T' : Time .
  var N N' : Nat .


  --- In the symbolic setting, physical envs have no timer. Also,
  --- executeStep is non-deterministic.
  crl [step] :
      {< E : Environment | >
       REACTORS-AND-CONNECTIONS
       < Q : EventQueue | queue : (EVENTS at tag(T,N)) ;; QUEUE >
       < RXNS : Invoked | >
       < SYM : SymInfra | constraint : CONST >} 
    =>
      {< E : Environment | >
       NEW-NETWORK
       < Q : EventQueue | queue : NEW-QUEUE >
       < RXNS : Invoked | reactions : INVOKED > 
       < SYM : SymInfra | constraint : CONST' && CONST'' >} 
   if CONST' := CONST && 
                boolToValue(minConst(tag(T,N),QUEUE) and T equals zero)
   /\ smtCheck(CONST')
   /\ executeStep(EVENTS, REACTORS-AND-CONNECTIONS, QUEUE, trueVal)
      => networkQueueRxns(NEW-NETWORK, NEW-QUEUE, INVOKED, CONST'') 
   /\ smtCheck(CONST' && CONST'') .

  op minConst : Tag EQueue ~> Bool .
  eq minConst(tag(T,N), (EVENTS at tag(T',N')) ;; QUEUE)
   = ((T lt T') or ((T equals T') and N <= N')) and minConst(tag(T,N), QUEUE) .
  eq minConst(tag(T,N), empty) = true .
endom


*** Now we describe tick rules for symbolic analysis. 

omod SYM-REACH-DYNAMICS is
  including DYNAMICS-WITHOUT-TICK .

  vars REACTORS-AND-CONNECTIONS CONF : Configuration .
  vars CONST CONST' : BoolValue .
  vars T T' : Time .
  var TI : TimeInf .
  vars E Q PA SYM RXNS : Oid .
  var N : Nat .
  var EVENTS : Events .
  vars QUEUE NEW-QUEUE : EQueue .
  var NZN : NzNat .
  var REACTORID : ReactorId .
  var TIMERID : TimerId .

  --- A tick rule for non-physical events. This rule advances time
  --- until the first queue item is ready to execute.  Note that time 
  --- values in the event queue may be symbolic
   crl [tick] :
       {< E : Environment | physicalActions : CONF >
        REACTORS-AND-CONNECTIONS
        < Q : EventQueue | queue : (EVENTS at tag(T,N)) ;; QUEUE >
        < RXNS : Invoked | >
        < SYM : SymInfra | constraint : CONST >} 
    =>
       {< E : Environment | physicalActions : increaseClock(CONF, T) >
        REACTORS-AND-CONNECTIONS
        < Q : EventQueue | queue : (EVENTS at tag(zero,N)) ;; 
                                   decreaseTags(QUEUE,T) >
        < RXNS : Invoked | reactions : none >
        < SYM : SymInfra | constraint : CONST' >} 
       in time T
    if CONST' := CONST &&
                 boolToValue(minConst(tag(T,N),QUEUE) and T gt zero) 
    /\ smtCheck(CONST') .

    op tv : Nat -> SMTVarId . --- SMT variable ids for time

    --- A tick rule for physical events. This rule advances time
    --- and ``fires'' a single physical event. 
    crl [extraTickRuleForPhysActs] :
       {< E : Environment | physicalActions : CONF >
        REACTORS-AND-CONNECTIONS
        < Q : EventQueue | queue : QUEUE >
        < RXNS : Invoked | >
        < SYM : SymInfra | constraint : CONST, counter : N >} 
     =>
       {envFire(< E : Environment | 
                    physicalActions : increaseClock(CONF, T) >)
        REACTORS-AND-CONNECTIONS
        < Q : EventQueue | queue : decreaseTags(QUEUE,T) >
        < RXNS : Invoked | reactions : none >
        < SYM : SymInfra | constraint : CONST', counter : s N >} 
     in time T
     if T := valueToTime(rr(tv(N)))    --- a fresh variable
     /\ CONST' := CONST &&  
                  boolToValue(minConst(tag(T,0),QUEUE) and T gt zero)
     /\ smtCheck(CONST') .

  --- Assume that T <= T' for any T'.
  op decreaseTags : EQueue Time -> EQueue .
  eq decreaseTags(empty, T) = empty .
  eq decreaseTags((EVENTS at tag(T', N)) ;; QUEUE, T) =
     (EVENTS at tag(T' monus T, N)) ;; decreaseTags(QUEUE, T) .

  --- increase clocks in physical action objects
  op increaseClock : Configuration Time ~> Configuration .
  eq increaseClock(none, T) = none .
  eq increaseClock(< PA : PhysAct | clock : TI > CONF, T)
   = < PA : PhysAct | clock : TI plus T >  increaseClock(CONF, T) .
endom

