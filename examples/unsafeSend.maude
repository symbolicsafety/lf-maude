
omod UNSAFE-SEND is 
  including LF-REPR .
  protecting LF-VALUE-TIME .
  protecting RUNTIME-APG .

  ops req error : -> RVarId [ctor] .
  ops client server : -> ReactorId [ctor] .
  ops in out : -> RPortId [ctor] .
  ops startup err : -> RActionId [ctor] .
  op init : -> Configuration .

  eq init
   = < client : Reactor |
          inports : < in : Port | value : [0] >,
	      outports : < out : Port | value : [0] >,
	      state : req |-> [0],
	      timers : none,
	      actions : < startup : LogicalAction | minDelay : 0, 
                                                minSpacing : 0, 
                                                policy : defer, 
                                                payload : [0] >,
	      reactions :
	        (reaction when startup --> out do { (req := [0]) ; (out <- req) })
             reaction when in do { req := [0] } 
      >
            
      < server : Reactor |
          inports : < in : Port | value : [0] >,
	      outports : < out : Port | value : [0] >,
	      state : error |-> [0],
	      timers : none,
	      actions : < err : LogicalAction | minDelay : 1, 
                                            minSpacing : 0, 
                                            policy : defer, 
                                            payload : [0] >,
	      reactions : 
	        (reaction when in --> out ; err do { 
	            if (in === [0]) then schedule(err,[0],[0])
	            else (out <- in) fi
	         })
	         reaction when err do { error := [1] }
      > 
            
      (client : out -- 2 --> server : in)
       server : out -- 2 --> client : in .
endom

omod TEST-UNSAFE-SEND is
  including UNSAFE-SEND .
  including DYNAMICS-WITHOUT-TICK .

  ops env queue rxns : -> Oid [ctor] .

  op initSystem : -> GlobalSystem .
  eq initSystem =
      {< env : Environment | physicalActions : none >
       addReactionIndices(init)
       < queue : EventQueue | queue : addStartup(startup, init, empty) >
       < rxns : Invoked | reactions : none >} .
endom


***( the main property in the LF repository is

spec="(F[0, 5 nsec](UnsafeSend_c_reaction_1))", expect=false

)***

omod TIME-BOUNDED-UNSAFE-SEND is
  including TEST-UNSAFE-SEND .
  including TIME-BOUNDED-DYNAMICS .
  eq timeBound = 5 .
endom  



omod MODEL-CHECK-UNSAFE-SEND is
  including MODEL-CHECKER .
  including TIME-BOUNDED-UNSAFE-SEND .
  including LF-PROP .
endom

red modelCheck(initSystem in time 0, <> ((client . 2) invoked)) .

***(    Result:
reduce in MODEL-CHECK-UNSAFE-SEND : modelCheck(initSystem in time 0, <> reaction client . 2 invoked) .
rewrites: 207 in 1ms cpu (0ms real) (207000 rewrites/second)
result ModelCheckResult: counterexample({{< client : Reactor | inports :
    < in : Port | value : [0] >, outports : < out : Port | value : [0] >, state : req |-> [0], reactions : (reaction 1 when startup --> out do{(req := [0]) ; out <- req} reaction 2
    when in --> none do{req := [0]}), timers : none, actions : < startup : LogicalAction | minDelay : 0, minSpacing : 0, policy : defer, payload : [0] > > < server : Reactor | inports
    : < in : Port | value : [0] >, outports : < out : Port | value : [0] >, state : error |-> [1], reactions : (reaction 1 when in --> out ; err do{if in === [0] then schedule(err, [
    0], [0]) else out <- in fi} reaction 2 when err --> none do{error := [1]}), timers : none, actions : < err : LogicalAction | minDelay : 1, minSpacing : 0, policy : defer, payload
    : [0] > > < env : Environment | physicalActions : none > < queue : EventQueue | queue : empty > < rxns : Invoked | reactions : (server . 2) > (client : out -- 2 --> server : in)
    server : out -- 2 --> client : in}  in time 3,deadlock})
)***
